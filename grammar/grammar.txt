Program ⟶ Declaration-list
Declaration-list ⟶ Declaration Declaration-list | EPSILON
Declaration ⟶ Declaration-initial Declaration-prime
Declaration-initial ⟶ Type-specifier ID #p_id
Declaration-prime ⟶ #declare_func_start Fun-declaration-prime | Var-declaration-prime
Var-declaration-prime ⟶ ; #declare_var | [ NUM #p_num ] #declare_array ;
Fun-declaration-prime ⟶ ( Params ) Compound-stmt #declare_func_end
Type-specifier ⟶ int | void
Params ⟶ int ID #p_id Param-prime Param-list | void
Param-list ⟶ , Param Param-list | EPSILON
Param ⟶ Declaration-initial Param-prime
Param-prime ⟶ [ ] #add_array_param | #add_int_param EPSILON
Compound-stmt ⟶ { #scope_enter Declaration-list Statement-list #scope_exit }
Statement-list ⟶ Statement Statement-list | EPSILON
Statement ⟶ Expression-stmt | Compound-stmt | Selection-stmt | Iteration-stmt | Return-stmt
Expression-stmt ⟶ Expression ; #exp_end | break ; #break | ;
Selection-stmt ⟶ if ( Expression ) #save Statement #jpf_save else Statement #jp
Iteration-stmt ⟶ repeat #label #loop_start Statement #loop_end until ( Expression ) #jp_break #jpf
Return-stmt ⟶ return Return-stmt-prime
Return-stmt-prime ⟶ ; | Expression ;
Expression ⟶ Simple-expression-zegond | ID #p_id B
B ⟶ = Expression #assign | [ Expression #call_index ] H | Simple-expression-prime
H ⟶ = Expression #assign | G D C
Simple-expression-zegond ⟶ Additive-expression-zegond C
Simple-expression-prime ⟶ Additive-expression-prime C
C ⟶ Relop Additive-expression #compare | EPSILON
Relop ⟶ < #lt | == #eq
Additive-expression ⟶ Term D
Additive-expression-prime ⟶ Term-prime D
Additive-expression-zegond ⟶ Term-zegond D
D ⟶ Addop Term #arithmetic D | EPSILON
Addop ⟶ + #add | - #sub
Term ⟶ Factor G
Term-prime ⟶ Factor-prime G
Term-zegond ⟶ Factor-zegond G
G ⟶ * #mult Factor #arithmetic G | EPSILON
Factor ⟶ ( Expression ) | ID #p_id #func_call_args_start Var-call-prime #func_call_args_end | NUM #p_num
Var-call-prime ⟶ ( Args ) #output | Var-prime
Var-prime ⟶ [ Expression #call_index ] | EPSILON
Factor-prime ⟶ #func_call_args_start ( Args ) #func_call_args_end #output | EPSILON
Factor-zegond ⟶ ( Expression ) | NUM #p_num
Args ⟶ Arg-list | EPSILON
Arg-list ⟶ Expression Arg-list-prime
Arg-list-prime ⟶ , Expression Arg-list-prime | EPSILON